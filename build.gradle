task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        maven {
            url 'http://mvn.slimjars.com'
        }
        mavenCentral()
    }
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

if (project.hasProperty("topobyte")) {
    subprojects {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }
}

subprojects {
    task('foo') << {
        def global = new File('info.pom');
        def local = new File(project.projectDir, 'info.pom');

        def pomInfo = new XmlSlurper().parse(global);
        if (local.exists()) {
            println pomInfo
            def pomInfo2 = new XmlSlurper().parse(local);
            pomInfo.children() << pomInfo2.children()
            println pomInfo2
            println pomInfo
        }

        println pomInfo.name
    }
}

subprojects {
    def global = new File('info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        pomInfo.name = pomInfoL.name
    }

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom.project {
                        name pomInfo.name
                        description pomInfo.description
                        url pomInfo.url
                    }
                }
            }
        }
    }
}

project(':jts-drawing-core') {
    dependencies {
        compile 'de.topobyte:jts-utils:0.1.1'
        compile 'de.topobyte:chromaticity-core:0.0.2'
        compile 'com.vividsolutions:jts:1.13'
    }
}

project(':jts-drawing-util') {
    dependencies {
        compile project(':jts-drawing-core')
    }
}

project(':jts-drawing-awt') {
    dependencies {
        compile project(':jts-drawing-core')
        compile 'de.topobyte:jgs:0.0.1'
        compile 'de.topobyte:jts2awt:0.0.1'
        compile 'de.topobyte:awt-utils:0.0.3'
        compile 'de.topobyte:chromaticity-awt:0.0.2'
    }
}

project(':jts-drawing-batik') {
    dependencies {
        compile project(':jts-drawing-core')
        compile project(':jts-drawing-awt')
        compile 'de.topobyte:jgs:0.0.1'
        compile 'org.apache.xmlgraphics:batik-svggen:1.7'
        compile 'org.apache.xmlgraphics:batik-dom:1.7'
    }
}
